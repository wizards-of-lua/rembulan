plugins {
  id 'java-library'
  id 'maven-publish'
  id 'eclipse'
}

subprojects {
  group = 'net.wizardsoflua.rembulan'
  version = '0.3.0'

  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'eclipse'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(8)
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  tasks.register('sourceJar', Jar) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allSource)
  }

  tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from(tasks.javadoc)
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from(components.java)
        artifact(tasks.named('sourceJar'))
        artifact(tasks.named('javadocJar'))
      }
    }
    repositories {
      maven {
        url = uri(findProperty('repoUrl') ?: 'https://repo.maven.apache.org/maven2')
        credentials {
          username = findProperty('repoUser')
          password = findProperty('repoPass')
        }
      }
    }
  }
}

